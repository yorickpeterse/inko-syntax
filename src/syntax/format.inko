# Formatting of token streams as HTML.
import builder.html.Document
import std.iter.Iter
import std.string.StringBuffer
import syntax.(Lexer, Token, TokenKind)

# A type for formatting a token stream into a `String`.
trait pub Format {
  # Formats the token stream produced by a `Lexer` into a `String`.
  #
  # We list `Iter[Token]` as an explicit requirement here due to
  # https://github.com/inko-lang/inko/issues/620.
  fn pub format[I: mut + Lexer + Iter[Token]](lexer: I) -> String
}

# A type that forms a lexer's token stream as HTML.
#
# The generated HTML is compatible with [Pygments](https://pygments.org/)
# stylesheets.
class pub Html {
  # A base/generic class to apply to the surrounding `<div>` and `<pre>`
  # elements.
  #
  # This field defaults to "highlight".
  let pub @class: Option[String]

  # An extra class to apply to the `<pre>` element.
  #
  # This class can be used to attach a language specific class, such as "bash"
  # when formatting Bash code.
  #
  # By default, no extra class is added.
  let pub @pre_class: Option[String]

  # Returns a new HTML formatter.
  fn pub static new -> Html {
    Html { @class = Option.Some('highlight'), @pre_class = Option.None }
  }
}

impl Format for Html {
  fn pub format[I: mut + Lexer + Iter[Token]](lexer: I) -> String {
    let doc = Document.new
    let div = doc.div
    let pre = div.pre
    let code = pre.code

    match (@class, @pre_class) {
      case (Some(a), Some(b)) -> {
        div.attr('class', a)
        pre.attr('class', "{a} {b}")
        nil
      }
      case (Some(a), _) -> {
        div.attr('class', a)
        pre.attr('class', a)
      }
      case (_, Some(b)) -> pre.attr('class', b)
      case _ -> {}
    }

    lexer.each fn (tok) {
      # These classes are taken from
      # https://github.com/pygments/pygments/blob/8f3bec7982ba52915ee95f34f18e188243364600/pygments/token.py.
      let cls = match tok.kind {
        case Comment -> 'c'
        case String -> 's'
        case Int -> 'mi'
        case Float -> 'mf'
        case Keyword -> 'k'
        case Field -> 'vi'
        case Symbol or Macro -> 'ss'
        case Text -> ''
      }

      let el = if cls.size > 0 { code.span.attr('class', cls) } else { code }

      el.text(lexer.buffer.slice(tok.start, tok.size))
    }

    doc.fragment = true
    doc.to_string
  }
}
